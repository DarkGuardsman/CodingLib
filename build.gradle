import java.text.SimpleDateFormat

plugins{
    id 'java'
    id 'idea'
    id 'maven-publish'
    id "io.freefair.lombok" version "5.3.0"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

group='com.darkguardsman'
version = '0.3.0'

repositories {
    mavenCentral()
}

dependencies{
    compile 'com.google.guava:guava:11.0.2'
    compile 'org.scala-lang:scala-library:2.11.1'
    compile "io.netty:netty-all:4.0.10.Final"
    compile "org.apache.logging.log4j:log4j-core:2.0-beta9"

    //Junit5
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    useJUnitPlatform()
}

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

jar {
    manifest {
        attributes(
                "Built-By": System.getProperty('user.name'),
                "Build-Timestamp": new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                "Build-Revision": getGitHash(),
                "Created-By": "Gradle ${gradle.gradleVersion}",
                "Build-Jdk": "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                "Build-OS": "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}",
                "Implementation-Title": project.name,
                "Implementation-Version": version,

        )
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/darkguardsman/codinglib")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            artifactId 'coding-lib'
            from(components.java)
        }
    }
}